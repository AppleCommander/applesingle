plugins {
    id 'org.springframework.boot' version "2.7.18"
    id 'java'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.2'
}

java {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'info.picocli:picocli:4.7.7'
    implementation project(':applesingle-api')
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'applesingle',
                'Implementation-Version': "${project.version} (${new Date().format('yyyy-MM-dd HH:mm')})"
        )
    }
}

application {
    mainClass = "io.github.applecommander.applesingle.tools.asu.Main"
}

bootJar {
    manifest {
        attributes(
                'Implementation-Title': 'applesingle',
                'Implementation-Version': "${project.version} (${new Date().format('yyyy-MM-dd HH:mm')})"
        )
    }
}

graalvmNative {
    String osName = System.getProperty('os.name').toLowerCase().split()[0]
    String osArch = System.getProperty('os.arch').toLowerCase().split()[0]
    binaries {
        named("main") {
            imageName.set("asu-${osName}-${osArch}-${project.version}")
        }
    }
}
